/*
 *  TOPPERS/HRP Kernel
 *      Toyohashi Open Platform for Embedded Real-Time Systems/
 *      High Reliable system Profile Kernel
 * 
 *  Copyright (C) 2008-2012 by Embedded and Real-Time Systems Laboratory
 *              Graduate School of Information Science, Nagoya Univ., JAPAN
 * 
 *  上記著作権者は，以下の(1)〜(4)の条件を満たす場合に限り，本ソフトウェ
 *  ア（本ソフトウェアを改変したものを含む．以下同じ）を使用・複製・改
 *  変・再配布（以下，利用と呼ぶ）することを無償で許諾する．
 *  (1) 本ソフトウェアをソースコードの形で利用する場合には，上記の著作
 *      権表示，この利用条件および下記の無保証規定が，そのままの形でソー
 *      スコード中に含まれていること．
 *  (2) 本ソフトウェアを，ライブラリ形式など，他のソフトウェア開発に使
 *      用できる形で再配布する場合には，再配布に伴うドキュメント（利用
 *      者マニュアルなど）に，上記の著作権表示，この利用条件および下記
 *      の無保証規定を掲載すること．
 *  (3) 本ソフトウェアを，機器に組み込むなど，他のソフトウェア開発に使
 *      用できない形で再配布する場合には，次のいずれかの条件を満たすこ
 *      と．
 *    (a) 再配布に伴うドキュメント（利用者マニュアルなど）に，上記の著
 *        作権表示，この利用条件および下記の無保証規定を掲載すること．
 *    (b) 再配布の形態を，別に定める方法によって，TOPPERSプロジェクトに
 *        報告すること．
 *  (4) 本ソフトウェアの利用により直接的または間接的に生じるいかなる損
 *      害からも，上記著作権者およびTOPPERSプロジェクトを免責すること．
 *      また，本ソフトウェアのユーザまたはエンドユーザからのいかなる理
 *      由に基づく請求からも，上記著作権者およびTOPPERSプロジェクトを
 *      免責すること．
 * 
 *  本ソフトウェアは，無保証で提供されているものである．上記著作権者お
 *  よびTOPPERSプロジェクトは，本ソフトウェアに関して，特定の使用目的
 *  に対する適合性も含めて，いかなる保証も行わない．また，本ソフトウェ
 *  アの利用により直接的または間接的に生じたいかなる損害に関しても，そ
 *  の責任を負わない．
 * 
 *  @(#) $Id: start.S 2268 2011-11-22 16:22:45Z ertl-honda $
 */
                
/*
 *   カーネル用のスタートアップモジュール(ARM-M用)
 */
#define TOPPERS_MACRO_ONLY
#define UINT_C(val)		(val)		/* uint_t型の定数を作るマクロ */
#define ULONG_C(val)	(val)		/* ulong_t型の定数を作るマクロ */
#define CAST(type, val)	(val)		/* 型キャストを行うマクロ */

#include "kernel_impl.h"
#include "arm_m.h"
#include "target_asm.inc"

/*
 *  スタートアップルーチン
 *
 *  Threadモードで呼び出されることを前提としてる．
 */
	ATEXT
	AALIGN(2)
	ATHUMB(_start)
	AGLOBAL(_start) 
ALABEL(_start)
	cpsid f                 /* 割込みロック状態へ */

#ifdef INIT_MSP
	/*
	 * MSPの設定
	 */
	mov   r0, #CONTROL_MSP  /* MSPを有効に  */
	msr   control, r0
	isb                     /* control の操作後に必要 */
	ldr  r0,=_kernel_istkpt
	ldr  r1,[r0]
	msr  msp, r1
#endif /* INIT_MSP */

	/*
	 * hardware_init_hookの呼び出し （0でない場合）
	 *
	 * ターゲットハードウェアに依存して必要な初期化処理がある場合
	 * は，hardware_init_hook という関数を用意する
	 */
	ldr  r0, =hardware_init_hook
	cbz  r0, start_1
	blx  r0

ALABEL(start_1)
#ifndef OMIT_BSS_INIT
	/*
	 *  BSSセクション初期化
	 */
	mov  r0, #0                         /* r0: 初期化する値 */
    ldr  r3, =_kernel_bsssecinib_table  /* r3: テーブルのアドレス */
    mov  r4, #0                         /* r4: テーブルのインデックス i */
    ldr  r5, =_kernel_tnum_bsssec
    ldr  r5, [r5]                       /* r5: テーブルの要素数 num */
ALABEL(start_bss_2)
    cmp  r4, r5         /* ループ終了条件: i >= num */
    bhs  start_3
    ldr  r1, [r3, #0]   /* r1: bssの先頭番地 */
    ldr  r2, [r3, #4]   /* r2: bssの終了番地 */
	cmp  r1, r2         /* bssが空か？ */
	bhs	 start_bss_4    /* 空ならば次のbssへ */
    nop
ALABEL(start_bss_3)
    /*
     *  bssの領域は，4byte境界にアラインされている
     *  よって，4byteずつデータをコピーしている
     */
	str  r0, [r1]       /* bssの領域を0で初期化 */
	add  r1, #4         /* 次の番地へ */
	cmp  r1, r2         /* bssの終了番地まで来たか？ */
	blo  start_bss_3    /* 終了番地でなければループ */
    nop
ALABEL(start_bss_4)
    add  r3, #8 /* テーブルのアドレスをインクリメント 4byte * 2 -> 8 */
    add  r4, #1 /* テーブルのインデックスをインクリメント: i++ */
    b    start_bss_2
    nop
#endif /* OMIT_BSS_INIT */


ALABEL(start_3)
#ifndef OMIT_DATA_INIT
	/*
	 *  DATAセクション初期化
	 *  __idata_start から __idata_end までを，__data_start 以降に
	 *  コピーする
	 */
    ldr  r3, =_kernel_datasecinib_table  /* r3: テーブルのアドレス */
    mov  r4, #0                         /* r4: テーブルのインデックス i */
    ldr  r5, =_kernel_tnum_datasec
    ldr  r5, [r5]                       /* r5: テーブルの要素数 num */
ALABEL(start_data_2)
    cmp  r4, r5         /* ループ終了条件: i >= num */
    bhs  start_5
    ldr  r1, [r3, #0] /* r1: dataの先頭番地 -> 初期化する変数アドレス */
    ldr  r2, [r3, #4] /* r2: dataの終了番地 */
    ldr  r6, [r3, #8] /* r6: idataの先頭番地 -> 初期化する定数値のアドレス */
	cmp  r1, r2         /* dataが空か？ */
	bhs	 start_data_4    /* 空ならば次のdataへ */
    nop
ALABEL(start_data_3)
    /*
     *  idataの領域は，ROM領域の節約のため，アラインされない
     *  よって，1byteずつデータをコピーしている
     */
    ldr  r0, [r6]       /* idataの値をロード */
    str  r0, [r1]       /* idataの値をdataのアドレスへストア */
	add  r1, #1         /* dataの次の番地へ */
	add  r6, #1         /* idataの次の番地へ */
	cmp  r1, r2         /* dataの終了番地まで来たか？ */
	blo  start_data_3   /* 終了番地でなければループ */
    nop
ALABEL(start_data_4)
    add  r3, #12 /* テーブルのアドレスをインクリメント 4byte * 3 -> 12 */
    add  r4, #1 /* テーブルのインデックスをインクリメント: i++ */
    b    start_data_2
    nop
#endif /* OMIT_DATA_INIT */

	/*
	 *  software_init_hook を呼出し（0 でない場合）
	 *
	 *  ソフトウェア環境（特にライブラリ）に依存して必要な初期化処
	 *  理がある場合は，software_init_hook という関数を用意すれば
	 *  よい．
	 */
ALABEL(start_5)
	ldr  r0, =software_init_hook
	cbz  r0, start_6
	blx  r0

ALABEL(start_6)
	bl   sta_ker

ALABEL(l_end)
	b   l_end
